#!/usr/bin/perl -w

use strict;
use List::Util qw[min max];

open(COMMAND, "qstat -r |");
my @jobs;
foreach my $line (<COMMAND>) {
    if ($line =~ m,^(\d+\.mgt)\s+,) {
	push @jobs, $1;
    }
}
close(COMMAND);

my $processors_per_node=8;
my $cluster_capacity=220*$processors_per_node;
my $allocated_count=0;
my $wasted_count=0;

foreach my $job (@jobs) {
    my %stats = get_job_stats($job);
    next if not %stats;
    next if not $stats{queue};
    next if ($stats{queue} ne 'dque'); #only look at the main queue.

    # this stuff is currently unused
    my %hostcount;
    if (defined $stats{exec_host}) {
	foreach my $hostspec (split(/\+/,$stats{exec_host})) {
	    my ($host, $proc) = split(m,/, , $hostspec);
	    $hostcount{$host} = 0 if not defined $hostcount{$host};
	    $hostcount{$host}++;
	}
    }

    #ssh into the nodes to find out if they are actually unused.
    my $unused_count=0;
    foreach my $machine (keys %hostcount) {
	#open(COMMAND, "ssh -n $machine 'grep -e \"^processor\" /proc/cpuinfo | wc -l ' |");
	#my $count = <COMMAND>;
	#chomp $count;
	#close(COMMAND);
	#next if $count !~ /^\d+$/;

	# leave one processor on each machine
	#$count--;

	my $count = $processors_per_node;
	
	open(COMMAND, "ssh -n $machine 'uptime' |") or next;
	foreach my $line (<COMMAND>) {
	    if ($line =~ /load average:\s+(\d*\.?\d+),\s+(\d*\.?\d+),\s+(\d*\.?\d+)/) {
		# $1= 1 min average, $2= 5 min average, $3= 15 min average.
		# $3 is probably the truest capture, but it fails for short jobs.
		# I'm going to suggest we just pick the max of all three-- this works
		# for short jobs and long jobs.  It also over compensates for bursty jobs,
		# yeilding more false negatives than false positives.
		my $load = max($1,$2,$3);
		$load += 0.70; # round up.
		$count -= int($load);
		last;
	    }
	}
	if ($count > 0) {
	    $unused_count+=$count;
	}
	close(COMMAND);
    }
    print "$stats{Job_Id}\t$stats{Job_Owner}\t$stats{Job_Name}\t$unused_count\n" if $unused_count;
    $wasted_count += $unused_count;
    $allocated_count += $processors_per_node*scalar(keys(%hostcount));
}
print "\n\nTotals:\n";
print sprintf("Processors allocated: %3d (%3.1f%%)\n", $allocated_count, 100.0*$allocated_count/$cluster_capacity);
print sprintf("Processors wasted:    %3d (%3.1f%%)\n", $wasted_count, 100.0*$wasted_count/$cluster_capacity);
exit(0);

sub get_job_stats {
    my ($job_id) = @_;
    my %stats;
    my $command;
    open($command, "qstat -f $job_id |") or return %stats;
    $stats{Job_Id} = $job_id;

    #search for exec host line
    my $found_exec_host=0;
    my $line=<$command>; #strip first line.
    $line=<$command>;
    while ($line) {
	chomp $line;
	if ($line =~ m,^\s+([A-Za-z_.]+) = (.*),) {
	    my $key = $1;
	    my $value = $2;
	    while($line = <$command>) {
		last if ($line !~ m,^\t,);
		chomp $line;
		$line =~ s/^\s+//;
		$line =~ s/\s+$//;
		$value .= $line;
	    }
	    $stats{$key} = $value;
	} else {
	    $line = <$command>;
	}
    }
    close($command);
    return %stats;
}

